{$CLEO .cs} 

{ 
by DK22Pac, Vol-GTA 
} 

// Настройки 
const 
       shadowRadius = 2.5 // Радиус проекции 
       shadowIntensity = 0xFF // Интенсивность проекции (0-255) 
end 

0000: 
while true 
       wait 0 
       if 
           not player.Defined(0) 
       then 
           continue 
       end 
       for 0@ = 0xC3E058 to 0xC3EF58 step 0x3C // перебор корон 
           0A8E: 8@ = 0@ + 0x38 // Указатель на Entity(игровой субьект), к которому прицеплена корона 
           0A8D: 1@ = read_memory 8@ size 4 virtual_protect false 
           0A8D: 28@ = read_memory 0xB74494 size 4 virtual_protect 0 // Указатель на первый элемент в пуле транспорта 
           0A8D: 28@ = read_memory 28@ size 4 virtual_protect 0    
           0A8D: 24@ = read_memory 0xB74494 size 4 virtual_protect false 
           inc(24@, 8) // Указатель на максимальное кол-во элементов в пуле транспорта 
           0A8D: 24@ = read_memory 24@ size 4 virtual_protect false    
           mul(24@, 0xA18) 
           005A: 24@ += 28@           
           if and // Если данный Entity - автомобиль... 
               002D: 1@ >= 28@ 
               8025: not 1@ > 24@ 
           then 
               0AEB: 7@ = car_struct 1@ handle 
               0A8E: 8@ = 0@ + 0x0 
               0A8D: 21@ = read_memory 8@ size 4 virtual_protect false // X позиция короны 
               inc(8@, 4) 
               0A8D: 22@ = read_memory 8@ size 4 virtual_protect false // Y позиция короны 
               inc(8@, 4) 
               0A8D: 23@ = read_memory 8@ size 4 virtual_protect false // Z позиция короны 
               0A8E: 8@ = 0@ + 0x2C 
               0A8D: 4@ = read_memory 8@ size 1 virtual_protect false // Red    
               inc(8@) 
               0A8D: 5@ = read_memory 8@ size 1 virtual_protect false // Green 
               inc(8@) 
               0A8D: 6@ = read_memory 8@ size 1 virtual_protect false // Blue    
               if 
                   23@ > 0.2 
               then 
                   if 
                       0137: car 7@ model == #COPBIKE // HPV1000 
                   then 
                       if and // Если это не корона передней фары 
                           21@ < 0.2 
                           21@ > -0.2 
                       then 
                           continue 
                       end 
                   end 
                   0407: store_coords_to 9@ 10@ 11@ from_car 7@ with_offset 21@ 22@ 23@ // Получаем мировые координаты                   
                   016F: add_shadow_type 3 rotation_factor 0.0 shadowRadius shadowIntensity RGB 4@ 5@ 6@ at 9@ 10@ 11@    
               end 
           end 
       end 
end